task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  classifier "sources"
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compileClasspath
  title = "Trusona SDK"
  options {
    memberLevel = JavadocMemberLevel.PUBLIC
    source = "1.8"
  }
}

task javadocZip(type: Zip) {
  dependsOn javadoc

  from "${javadoc.destinationDir}/.."
  into "${project.name}-${project.version}"
  archiveName = "${project.name}-${project.version}.zip"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar, javadocZip, sourceJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourceJar
      artifact javadocZip
      artifact javadocJar
    }
  }
}

signing {
  sign publishing.publications.mavenJava
  sign configurations.archives
}

dependencies {
  compile project(":trusona-sdk-http")
  compile project(":trusona-sdk-resources")

  testCompile "org.codehaus.groovy:groovy-all:2.5.3"
  testCompile "org.spockframework:spock-core:1.2-groovy-2.5"
  testCompile "com.squareup.okhttp3:mockwebserver:3.11.0"
  testCompile "ch.qos.logback:logback-classic:1.2.3"

  spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1"
}