buildscript {
  repositories {
    mavenCentral()
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.24.12"
    classpath "de.marcphilipp.gradle:nexus-publish-plugin:0.4.0"
    classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:2.0.0"
    classpath "com.google.guava:guava:30.1.1-jre"
  }
}

allprojects {
  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:all,-processing"
    }
    tasks.withType(GroovyCompile) {
      options.compilerArgs << "-Xlint:all,-processing"
    }
  }

  tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
    testLogging.showStandardStreams = false
  }
}

subprojects {
  group "com.trusona"
  version "4.3.1-SNAPSHOT"

  apply plugin: "java"
  apply plugin: "groovy"
  apply plugin: "signing"
  apply plugin: "maven"
  apply plugin: "maven-publish"
  apply plugin: "jacoco"
  apply plugin: "com.github.spotbugs"
  apply from: "../publishing.gradle"
  apply from: "../build-extras.gradle"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenCentral()
    jcenter()
  }

  task "printVersion" {
    doLast {
      println project.version
    }
  }

  test {
    testLogging {
      showStandardStreams = true
      events "PASSED", "FAILED", "SKIPPED"
    }

    finalizedBy spotbugsMain, jacocoTestReport
  }

  jacocoTestReport {
    reports {
      xml.enabled true
    }
  }
}