buildscript {
  repositories {
    mavenCentral()
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.33.20"
    classpath "de.marcphilipp.gradle:nexus-publish-plugin:0.4.0"
    classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.7.5"
    classpath "com.google.guava:guava:33.3.1-jre"
  }
}

allprojects {
  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:all,-processing"
    }
    tasks.withType(GroovyCompile) {
      options.compilerArgs << "-Xlint:all,-processing"
    }
  }
}

subprojects {
  group "com.trusona"
  version "4.4.0-SNAPSHOT"

  apply plugin: "java-library"
  apply plugin: "groovy"
  apply plugin: "signing"
  apply plugin: "maven-publish"
  apply plugin: "jacoco"
  apply plugin: "com.github.spotbugs"
  apply from: "../publishing.gradle"
  apply from: "../build-extras.gradle"

  sourceCompatibility = 1.11
  targetCompatibility = 1.11

  repositories {
    mavenCentral()
    jcenter()
  }

  task "printVersion" {
    doLast {
      println project.version
    }
  }

  test {
    useJUnitPlatform()

    testLogging {
      showStandardStreams = false
      events "PASSED", "FAILED", "SKIPPED"
    }

    finalizedBy spotbugsMain, jacocoTestReport
  }

  jacocoTestReport {
    reports {
      xml.enabled true
    }
  }
}