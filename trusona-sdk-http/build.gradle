task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  classifier "sources"
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compileClasspath
  title = "Trusona HTTP SDK"
  options {
    memberLevel = JavadocMemberLevel.PUBLIC
    source = "1.8"
  }
}

task javadocZip(type: Zip) {
  dependsOn javadoc

  from "${javadoc.destinationDir}/.."
  into "${project.name}-${project.version}"
  archiveName = "${project.name}-${project.version}.zip"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar, javadocZip, sourceJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourceJar
      artifact javadocZip
      artifact javadocJar
    }
  }
}

signing {
  sign publishing.publications.mavenJava
  sign configurations.archives
}

dependencies {
  implementation project(":trusona-sdk-resources")

  api "org.apache.commons:commons-lang3:3.16.0"
  api "commons-codec:commons-codec:1.17.1"
  api "com.squareup.retrofit2:retrofit:2.11.0"
  api "com.squareup.retrofit2:converter-jackson:2.11.0"
  api "com.squareup.okhttp3:logging-interceptor:4.12.0"
  api "org.slf4j:slf4j-api:2.0.16"

  testImplementation "org.apache.groovy:groovy-all:4.0.22"
  testImplementation "org.spockframework:spock-core:2.3-groovy-4.0"
  testImplementation "ch.qos.logback:logback-classic:1.5.11"
  testImplementation "com.squareup.okhttp3:mockwebserver:4.12.0"
  testImplementation "com.squareup.retrofit2:retrofit-mock:2.11.0"

  spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0"
}