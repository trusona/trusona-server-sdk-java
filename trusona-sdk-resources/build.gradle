task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  classifier "sources"
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compileClasspath
  title = "Trusona Resources SDK"
  options {
    memberLevel = JavadocMemberLevel.PUBLIC
    source = "1.8"
  }
}

task javadocZip(type: Zip) {
  dependsOn javadoc

  from "${javadoc.destinationDir}/.."
  into "${project.name}-${project.version}"
  archiveName = "${project.name}-${project.version}.zip"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar, javadocZip, sourceJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourceJar
      artifact javadocZip
      artifact javadocJar
    }
  }
}

signing {
  sign publishing.publications.mavenJava
  sign configurations.archives
}

dependencies {
  compile "org.apache.commons:commons-lang3:3.11"
  compile "commons-codec:commons-codec:1.15"
  compile "com.fasterxml.jackson.core:jackson-databind:2.9.8"
  compile "org.slf4j:slf4j-api:1.7.30"

  testCompile "org.codehaus.groovy:groovy-all:2.5.3"
  testCompile "org.spockframework:spock-core:1.3-groovy-2.5"
  testCompile "ch.qos.logback:logback-classic:1.2.3"

  spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1"
}