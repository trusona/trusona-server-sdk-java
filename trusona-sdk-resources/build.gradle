task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  classifier "sources"
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compileClasspath
  title = "Trusona Resources SDK"
  options {
    memberLevel = JavadocMemberLevel.PUBLIC
    source = "1.8"
  }
}

task javadocZip(type: Zip) {
  dependsOn javadoc

  from "${javadoc.destinationDir}/.."
  into "${project.name}-${project.version}"
  archiveName = "${project.name}-${project.version}.zip"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar, javadocZip, sourceJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourceJar
      artifact javadocZip
      artifact javadocJar
    }
  }
}

signing {
  sign publishing.publications.mavenJava
  sign configurations.archives
}

dependencies {
  api "org.apache.commons:commons-lang3:3.13.0"
  api "commons-codec:commons-codec:1.16.0"
  api "com.fasterxml.jackson.core:jackson-databind:2.15.2"
  api "org.slf4j:slf4j-api:2.0.9"

  testImplementation "org.apache.groovy:groovy-all:4.0.14"
  testImplementation "org.spockframework:spock-core:2.3-groovy-4.0"
  testImplementation "ch.qos.logback:logback-classic:1.4.11"

  spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0"
}